title: InAratelWarp
---
<<placeActor "Ambassador G'Vorik" GVorikQuarters>>
<<moveTo "Ambassador G'Vorik" CaptainCabin1>>
Commander Corwin: Computer estimates about 2 minutes until arrival.
Officer Roquell: Ambassador G'Vorik has your mission brief and is in your quarters, Captain.
-> You: Thank you.
    <<enableTrigger CaptainCabinTrigger AW_GVorik>>
-> You: Mind showing me the way?
    <<escort "Officer Roquell" CaptainCabin Player 10>>
    <<enableTrigger CaptainCabinTrigger AW_ArriveAtQuarters>>
===
title: AW_ArriveAtQuarters
---
-> You: Why is the door see-through?
    Officer Roquell: You can darken it if you get a membranic technician to look at it.
    -> You: And where is my bed?
        Officer Roquell: I guess your room doesn't come with one. We can pick one up as soon as possible.
        Ambassador G'Vorik: If it makes you feel any better, my room doesn't come with a bed.
    -> You: Ah.
        Ambassador G'Vorik: There's no toilet on this deck. So that's a mixed bag.
-> You: Where is my bed?
    Officer Roquell: Not sure. We can pick one up later.
    -> You: And why is the door see-through?
        Officer Roquell: It's adjustable. A membranic technician should be able to fix it.
        Ambassador G'Vorik: There's no toilet on this deck. So that's a mixed bag.
    -> You: Ah.
        Ambassador G'Vorik: If it makes you feel any better, my room doesn't come with a bed.
-> You: Thank you.
<<moveTo "Officer Roquell" XOStation>>
<<jump AW_GVorik>>
===
title: AW_GVorik
---
<<set $gvorikStatus to "onBridge">>
Ambassador G'Vorik: Captain. Here's your mission brief.
<<addDataBlock "Aratel Brief">>
Ambassador G'Vorik: I transferred it to your A.C.E.S system under "Data". You can access it by pressing "TAB" or "ESC". I'll give you a censecond to read it.
<<wait 2>>
Ambassador G'Vorik: Okay. Let's talk strategy.
-> You: Let's.
    <<jump AW_Strategy>>
-> You: Strategy? Who needs strategy?
    <<jump AW_NoStrategy>>
===
title: AW_Strategy
---
Ambassador G'Vorik: Strategy. Where to start? The one thing we know about the Aratel for certain is that they are a religious order.
Ambassador G'Vorik: Which is strange. Why ask for Union intervention? Religious systems tend to resolve matters internally, whether that be by revolution or... mass execution.
Ambassador G'Vorik: I think the Aratel are in the midst of a *radical* cultural evolution. I have no clue as to how long it's been going on for, but long enough that both sides have been exhausted enough to ask for intervention.

-> You: What can you tell me about the revolution?
    Ambassador G'Vorik: That's the even weirder part. We know both sides have agreed to a ceasefire in advance for our negotiations, but...
    Ambassador G'Vorik: We've picked up bursts of weapons fire from the Aratel system through subspace.
    Ambassador G'Vorik: And we are in wild speculation territory now, but I suspect there might be more than a few radical third parties intentionally making things worse.
    Ambassador G'Vorik: But we don't have enough information on the Aratel to extrapolate any further, which brings me to our strategy. 
    -> You: None of this was mentioned in the brief.
        Ambassador G'Vorik: The most important thing that anyone can know about the Aratel is their surprising advancement and sudden conflict. It's so you don't get caught off guard.
        Ambassador G'Vorik: Specifics don't matter as much unless you're at the negotiating table. And whoever wrote your report didn't expect the Captain to have a direct part in negotiations.
        Ambassador G'Vorik: But as I was saying...
    -> You: Go on.
-> You: So what's the plan?

Ambassador G'Vorik: Do whatever you can to encourage peaceful cooperation when we arrive. Tensions are bound to be high, and we need to do anything and everything to defuse them.
Ambassador G'Vorik: Secondly, gather as much information on the Aratel as much as you can. Monitor communications, scan their ships, whatever you can do.
Ambassador G'Vorik: We only have snippets of information as to what this conflict is really about. The more we understand the root causes, the better I can work with both sides to stop it.
Ambassador G'Vorik: I wish there was more I could give you, but that's it as far as relevant information goes.
-> You: Understood.
-> You: What a crock of tanj.
    Ambassador G'Vorik: What? Captain...
    -> You: That was so long and boring.
    -> You: Your strategy sucks.
    -> You: Nevermind.
        <<set $gvorikStatus to "onBridge">>
        <<jump AW_ExitWarp>>

    Ambassador G'Vorik: Listen, I had 10 minutes to come up with a plan to prevent all out war with the Aratel. This is as good as it gets.
    -> You: New plan. My plan.
        Ambassador G'Vorik: Captain, please.

        <<set $gvorikStatus to "independent">>
        -> You: LA LA LA. NOT LISTENING.
            <<jump AW_ExitWarp>>
        -> You: I gave you a chance. I heard you out, and I'm saying no.
            <<jump AW_ExitWarp>>
        -> You: Fine.
    -> You: It can get significantly better. You're confined to quarters.
        <<if $gvorikStatus == "confined">>
            Ambassador G'Vorik: You just tried pulling this!
        <<else>>
            Ambassador G'Vorik: What? You can't do that.
            <<set $gvorikStatus to "confined">>
        <<endif>>

        Ambassador G'Vorik: I'm not listening anyways. My strategy will work. I just need your compliance. Please, Captain.
        -> You: Remember when I said I was kidding? I wasn't. <<if $gvorikStatus == "confined">>
            <<jump AW_ExitWarp>>
        -> You: You know where you can shove your "compliance"?
            Ambassador G'Vorik: When this is over, I will have your career.
            <<jump AW_ExitWarp>>
        -> You: ... Fine.
    -> You: We'll give it a try. 
        Ambassador G'Vorik: Thank you, Captain.
<<set $gvorikStatus to "onBridge">>
<<jump AW_ExitWarp>>
===
title: AW_NoStrategy
---
Ambassador G'Vorik: Listen, I wouldn't need to include you in these discussions if we weren't catapulting into an unknown situation.
Ambassador G'Vorik: The Aratel are in the midst of revolution, and we need to figure out how to work together to see a peaceful outcome. 
-> You: Okay, I see your point.
    <<jump AW_Strategy>>
-> You: That's dumb. Your idea is dumb.
Ambassador G'Vorik: {$name}, I need you to be on board with this.
-> You: Okay. That convinced me.
    Ambassador G'Vorik: Really?
    -> You: No.
        Ambassador G'Vorik: Who made you captain?
    -> You: Yes.
        <<jump AW_Strategy>>
-> You: Nope, I'm gonna wing it.

Ambassador G'Vorik: Okay. Fine! I have plans, I'll just have to make them without you, Captain.

-> You: Fine then.
    <<set $gvorikStatus to "independent">>
    <<jump AW_ExitWarp>>
-> You: No you won't. I'm confining you to quarters.
    <<set $gvorikStatus to "confined">>
    Ambassador G'Vorik: What? You can't do that.
    -> You: Yes I can, I'm the Captain.
        Ambassador G'Vorik: You're the worst Captain I've ever had the displeasure of working with.
        Ambassador G'Vorik: When my superiors hear about this, you're going to regret it. Your career will be over.
        -> You: Kidding! Ha.
            Ambassador G'Vorik: That's not a funny joke.
            -> You: Ha. Funny joke. Let's talk strategy.
                <<jump AW_Strategy>>
            -> You: Yes indeed. Still gonna wing it, though. Don't need a plan.
                <<set $gvorikStatus to "independent">>
                Ambassador G'Vorik: Okay.
                <<jump AW_ExitWarp>>
        -> You: Fat chance.
            <<jump AW_ExitWarp>>
    -> You: I could kill you. Your choice.
        Ambassador G'Vorik: WHAT?
        Ambassador G'Vorik: ...
        Ambassador G'Vorik: Fine.
    <<lockDoor QuarterDoor true>>
    <<jump AW_ExitWarp>>
===
title: AW_ExitWarp
---
Commander Corwin: We're about to exit Subspace, Captain. We need you on the bridge.[Comm/]
<<if $gvorikStatus == "onBridge">>
Ambassador G'Vorik: See you there, Captain.
<<moveTo "Ambassador G'Vorik" BridgeLeft>>
<<elseif $gvorikStatus == "independent">>
<<moveTo "Ambassador G'Vorik" BridgeLeft>>
<<elseif $gvorikStatus == "confined">>
<<moveTo "Ambassador G'Vorik" GVorikQuarters>>
<<endif>>

<<enableTrigger BridgeTrigger AW_ActualExit>>
===
title: AW_ActualExit
---
<<exitWarp>>
===
title: Introductions
---
// Scrapped, move this to conference room scene post Aratel arrival.
<<set $commanderCorwinAttitude to "provoke">>
-> You: That still leaves time for proper introductions.
    Officer Roquell: You already got part of the gist, but I am your First Officer.
    Officer Roquell: Our files are available in your quarters whenever you'd like to take a look.
    Commander Corwin: First Officer, you have to actually *introduce* yourself. Captain, I am Navigational Genius Wair Corwin. 100% human, 100% Earth-born, and 200% proud of it.
    -> You: Nice to make a proper introduction, Commander.
        Commander Corwin: And you too, Captain. I ever tell you that I almost crashed the ULSS Invincible?
        Commander Rikt: Almost killing me in the process.
        Commander Corwin: Which brings us to my buddy from the academy toiling away in the armpit of {$shipName}. Jesh.
        Commander Rikt: We *agreed* we wouldn't let you introduce us in tandem anymore. I'm Jesh _Rikt_, and *my ship* is not an armpit.
        Commander Corwin: I never called it an armpit, I said you were *working* in the armpit.
    -> You: Corwin, I expect a certain amount of deference among my crew. Respect your commanding officers.
        <<set $commanderCorwinAttitude to "defer">>
        Commander Corwin: Yes, Captain. Sorry, First Officer.
        Officer Roquell: Thank you, Commander Corwin.
        Commander Rikt: And I'm Jesh Rikt. Sorry I can't be on the bridge Captain, but the engines require near-constant care.
    Commander Rikt: I'm from Glxnbqwut. For humans like yourselves, we prefer it if you just say "Guh" whenever you give up trying to pronounce it correctly.
    -> You: And what is Gwlx... Guh like?
    -> You:   
-> You: Where's my mission brief?
    Officer Roquell: In your quarters, Captain.
===